# Вы можете расположить сценарий своей игры в этом файле.
# Определение персонажей игры.
label start:
    #call scene_0небольшая сводка о гг + выбор имеми для него
    $ name_mc = renpy.input("Как будут звать главную героиню?", length=12, default="Мейв", exclude="0123456789=+/.<>?,{}()&*^%$#@!:;|").strip()
    if name_mc == "":
        $ name_mc = "Мейв"
    call scene_1
    # call scene_2
    # call scene_3
    # call scene_4

    return

label scene_1:
    play music calm_music fadeout 1
    scene classroom
    pause 2.0
    # show gg обычная

    #$ name_mc = renpy.input("Как будут звать главную героиню?", length=12, default="Мейв", exclude="0123456789=+/.<>?,{}()&*^%$#@!:;|").strip()
    # if name_mc == "":
    #     $ name_mc = "Мейв"
    # hide gg
    # show gg радостная
    # "[name_mc] — девушка 17 лет, сталкивается с трудностями в выборе будущей профессии. Она обожает заниматься программированием, но не уверена, что направление программная инженерия подходит ей..."
    # hide gg 
    # with slowdissolve

    "Школа. Кабинет Русского языка."
    "[name_mc] сидит за партой в плохом настроении, так как ищет в телефоне информацию о Web-разработчике, но толком ничего не может найти"
    "К ней подсаживается её друг-одноклассник"

    show gg внедоумении at left
    with move
    show mike обычный at right

    mike обычный1 "— Привет! Можешь помочь с домашней работой…Эм, ты в порядке? Что-то случилось?"
    main_character удивленная1"— О, привет, [mike]! Я не уверена, что сделала правильный выбор предметов на экзамены. Или вообще своей будущей профессии…"
    mike обычный1 "— Погоди, разве ты не хотела поступать на программиста или вроде того?"
    main_character задумчивая1 "— На специальность «Программная инженерия», да. Это довольно широкое направление и я думала, что смогу разрабатывать сайты и приложения, заниматься их поддержкой."
    mike обычный1 "— Честно говоря, не звучит, как женская работа. Девушка-программист? Я только парней встречал."
    mike обычный1 "— Они, кажется, единственные, кто этим интересуются. Это же вроде как очень сложное дело, очень много кодировать нужно и знать не один язык программирования."

    menu:
        "Выбор ответа"
        "Я думала, мне хватит сил, чтобы осилить программирование. Но ты заставляешь меня задуматься об этом…":
            call friend_answer1("— Я думала, мне хватит сил, чтобы осилить программирование. Но ты заставляешь меня задуматься об этом…")
        "Я не думала, что программирование только для мужчин. Теперь я переживаю еще сильнее.":
            call friend_answer1("— Я не думала, что программирование только для мужчин. Теперь я переживаю еще сильнее.")
        "Не звучит, как женская работа?! Серьезно? По твоему женщины глупее мужчин? Да уж…":
            main_character внедоумении1 "— Не звучит, как женская работа?! Серьезно? По твоему женщины глупее мужчин? Да уж…"
            mike обычный1 "— Нет, что ты. Женщины не глупее мужчин. Просто я уверен, что это будет нелегко для тебя. В сфере IT большая конкуренция среди мужчин, не говоря уже о том, кому отдадут приоритет при приеме на работу."
    mike обычный1 "— Ты точно уверена, что программирование это твое?"

    menu:
        "Ты точно уверена, что программирование это твое?"
        "Нет…я не знаю.":  
            main_character задумчивая1 "— Нет…я не знаю."
        "Думаю, мне стоит подумать еще об этом.": 
            main_character задумчивая1 "— Думаю, мне стоит подумать еще об этом."
        "Как всё сложно в этом мире… Можно мне гайд на эту жизнь?":
            main_character задумчивая1 "— Как всё сложно в этом мире… Можно мне гайд на эту жизнь?"
    
    mike обычный1 "— Не переживай ты так, у тебя еще есть время подумать об этом. Сейчас главное сдать экзамены, верно?"

    menu:
        "Сейчас главное сдать экзамены, верно?"
        "Ты прав, нужно побеспокоиться пока об этом.":  
            main_character обычная1 "— Ты прав, нужно побеспокоиться пока об этом."
            mike обычный1 "— Кстати, я слышал, что завтра будет проходить день открытых дверей в IT-Академии. Возможно, там будут говорить про все твои “компьютерные” направления."
        "Точно, экзамены! Я же не успела вчера сделать 10 вариант по русскому языку. Дай списать, пожалуйста.":
            main_character испуганная1 "— Точно, экзамены! Я же не успела вчера сделать 10 вариант по русскому языку. Дай списать, пожалуйста."
            mike обычный1 "— Дак я у тебя хотел списать."
            main_character радостная1 ""
            mike обычный1 "— Ладно, у Миши попрошу. Кстати, я вспомнил! Кажется в IT-Академии будет проходить день открытых дверей. Это случайно не тот университет, в который ты хотела поступать?"
    main_character обычная1 "— Да, я слышала об этом. Надо будет сходить и узнать обо всём. Надеюсь, у меня получится после этого определиться…"
            
        
    hide mike
    with slowdissolve
    hide gg
    with slowdissolve
    
    jump scene_2

label friend_answer1(answer):
    main_character внедоумении1 "[answer]"
    mike обычный1 "— Не хотел тебя расстраивать, но я всё равно думаю, что это будет нелегко для тебя. В сфере IT большая конкуренция среди мужчин, не говоря уже о том, кому отдадут приоритет при приеме на работу."
    return 

label scene_2:
    scene laboratory
    with slowdissolve1
    "Научная лаборатория"

    show scientist обычный at left
    scientist обычный "— Так-с. Нейроникс, как у тебя продвигается работа?"

    show robot обычный1 at right
    "*Что-то печатает…*"
    robot обычный1 "— Я закончу скоро, сэр. Мне осталось чуть чуть."
    "*Продолжает печатать*"
    robot обычный1 "— Ошибка! Ошибка! Переизбыток информации!"
    "*Отключается*"

    scientist обычный1 "— Что происходит?!"
    "*Смотрит в экран*"
    scientist обычный1 "— Гениально!! Тебе удалось написать сайт, который получает доступ к \"Всемирной паутине разработчиков\". Но тут чего-то не хватает…"
    "*Пытается перезапустить робота*"
    scientist обычный1 "— Давай, работай! Только ты сможешь дописать сайт! Через \"дыру\" в сайте мы сможем получить всю логику программирования. Мы создадим такое, что весь мир будет у нас на ладони! Босс точно будет нами гордиться. Осталось только тебя починить."
    "*Пытается починить робота*"

    jump scene_3
    
label scene_3:
    scene university
    with slowdissolve1
    "IT-Академия \"Техно-WEB\""
    with slowdissolve

    scene conference hall
    "День открытых дверей."
    "Выступление директора IT-Академии перед абитуриентами."
    pause 1.5
    show senior_gpt обычный at center

    senior_gpt обычный "— Добрый вечер, уважаемые будущие студенты! Для меня большая честь стоять перед вами сегодня как избранный директор нашей престижной IT-Академии."
    senior_gpt ухмылка "— Позвольте представиться, меня зовут Senior Gpt, моей первостепенной обязанностью будет сделать это место лучше для каждого из вас."
    senior_gpt ухмылка "— В ближайшие месяцы всех нас ждут небольшие изменения в учебной программе, но не волнуйтесь, это будет увлекательное нововведение, одно из которых – это введение во всеобщее пользование роботов \"Нейроникс\"."

    show senior_gpt обычный1 at left
    with move
    show robot обычный at right

    senior_gpt обычный1 "— Вижу, многие недоумевают."
    senior_gpt обычный1 "— Я уверен, в этом зале сидит много талантливых и успешных учеников нашего будущего поколения, но, готов поспорить, каждому из вас приходило в голову, что было бы удобно ускорить работу и решать больше поставленных задач как программисты?"
    senior_gpt обычный1 "— И вот оно – решение перед вами! Роботы-помощники, чье развитие шагнуло далеко вперед."

    "*Удивление студентов*"

    senior_gpt обычный1 "— Также стоит сказать, что в новом учебном году некоторые направления подготовки будут или расформированы в связи с устаревшим учебным материалом."
    senior_gpt ухмылка1 "— К счастью для всех нас, Нейрониксы будут введены для дальнейшего использования! О дальнейших изменениях можно будет узнать на сегодняшних лекциях."

    "*Шептание и непонимание студентов*"

    hide robot обычный
    show senior_gpt ухмылка at center
    with move

    senior_gpt ухмылка "— Но не стоит волноваться! С развитием новых технологий появляется всё больше новых возможностей для самореализации. Каждый студент сможет найти свое место в сфере IT."
    senior_gpt обычный "— Благодарю за внимание, уважаемые слушатели, и с этого момента давайте же вместе двигаться вперед, к новым вершинам знаний и достижений!"

    "*Сдержанные аплодисменты*"


    jump scene_4

label scene_4:
    scene university_corridor
    with slowdissolve1

    "Коридор университета"
    "[name_mc] задумчиво идет по коридору"
    "Вдруг она встречает свою одноклассницу"

    show kate обычная

    kate "— [name_mc]! Привет!"

    "*[name_mc] поднимает на нее глаза*"

    show gg удивленная at left
    show kate обычная at right
    with move

    menu:
        "Ой, Катя, привет! Не знала, что ты тоже увлекаешься программированием.":
            main_character удивленная1 "— Ой, Катя, привет! Не знала, что ты тоже увлекаешься программированием."
            kate обычная1 "— Если WEB-дизайн можно назвать программированием, то да. Представляешь, я была на лекции, где рассказывали про Нейрониксов."
            kate обычная1 "— Так круто, что теперь не придется долго думать над концепцией сайта, приложения. товарной упаковки. За тебя это сделает Нейроникс!"
        "Катя?! Не ожидала тебя здесь увидеть! Ты тоже собираешься сюда поступать?":
            main_character удивленная1 "— Катя?! Не ожидала тебя здесь увидеть! Ты тоже собираешься сюда поступать?"
            kate обычная1 "— Да, я решила поступать на WEB-дизайнера. Я так рада, что появляются новые курсы и возможности для студентов."
            kate обычная1 "— На лекции нам рассказали, что с помощью Нейрониксов можно быстрее придумывать идеи для проектов и реализовывать их за считанные секунды. Представляешь?!"
    
    menu:
        "Звучит интересно. Как ты думаешь, это не \"убивает\" саму суть профессии?":
            main_character  задумчивая1 "— Звучит интересно. Как ты думаешь, это не \"убивает\" саму суть профессии?"
            kate обычная1 "— Ой, я как то об этом не задумывалась… хм…Мне нравится создавать что-то красивое и придумывать что-то новое. Я пытаюсь вкладывать душу в свои работы."
            kate обычная1 "— Поэтому думаю, что роботы только наталкивают на идею, а человек ее уже дорабатывает. Профессия обретает новую возможность для развития, но никак не теряет ее суть."
        "Ого, а ты не боишься, что некоторые профессии исчезнут из-за роботов?  И тогда многие останутся без работы.":
            main_character задумчивая1 "— Ого, а ты не боишься, что некоторые профессии исчезнут из-за роботов?  И тогда многие останутся без работы."
            kate обычная1 "— Мне кажется, что нам следует посмотреть на эти изменения с другой стороны. Мы же здесь для того, чтобы расширять свои горизонты и расти профессионально."
            kate обычная1 "— Если мы не будем открыты новым возможностям, будем боятся будущего, то можем упустить что-то очень ценное и увлекательное. Стоит воспользоваться шансом и не бояться его."
        
    main_character внедоумении1 "— Наверное, ты права… Стоит проще к этому отнестись…"
    main_character внедоумении1 "— Ой, кажется, я опаздываю на лекцию Ты случайно не знаешь. где находится аудитория…Э…секундочку…"

    "*Смотрит в телефон с расписанием лекций*"

    main_character внедоумении1 "— Римская два?"
    kate обычная1 "— К сожалению, не знаю. Удачи в поисках!"
    main_character внедоумении1 "— Спасибо. Пока!"

    jump scene_5


label scene_5:
    scene university_corridor
    with slowdissolve1

    "Переход к этой сцене"
    "*[name_mc] стучиться в дверь*"
    "*Тук-тук*"

    with slowdissolve
    scene laboratory
    show gg внедоумении at left
    main_character "— Здравствуйте, я тут, кажется, заблудилась. Вы не подскажете, как можно пройти до римской два?"

    show robot обычный at right
    robot обычный1"— …"

    "*Подходит ближе*"

    main_character обычная1 "— Хм, неразговорчивый Нейроникс попался?… Стоп, а где я вообще?!"

    "*Смотрит по сторонам*"

    hide robot 
    show gg обычная at center
    with move
    main_character обычная "— Жуткое место. так много роботов в одном месте я еще не видела, дак еще и неживых."
    main_character обычная "— Так, ладно. Надо найти римскую два."

    "*В этот момент по ее руке начинает ползти радиопаук*"

    main_character испуганная "— АААА, паук!"

    "*[name_mc] пугается, поэтому начинает смахивать паука с руки, но тот, чтобы не упасть с такой большой высоты  кусает ее за руку*"

    main_character испуганная "— Ай!"

    "*Хлоп*"
    "*[name_mc] прихлопнула паука*"

    main_character испуганная "— Радиопаук? Так он был ненастоящий?! Еще и укусить успел… Очень надеюсь, что никто этого не видел. Наверное, это была новая разработка университета, а я его сломала…Нужно быстрее уходить отсюда."

    "*[name_mc] осторожно выходит из лаборатории*"

    with slowdissolve
    scene university_corridor

    "*Ищет выход из здания*"


    jump scene_6

label scene_6:
    scene apartment
    with slowdissolve1

    "*[name_mc] вернулась домой*"
    "*В прихожей ее встречает мама*"

    show mother обычная1
    mother обычная1 "— [name_mc], ты уже вернулась?! Как всё прошло?"

    show gg обычная1 at left
    show mother обычная at right
    with move

    main_character обычная1 "— Привет, мам… Да как тебе сказать.. Если честно, ужасно."
    mother обычная1 "— Ты какая-то расстроенная. Что-то случилось?!"
    
    menu:
        "Что-то случилось?!"
        "После этого дня я еще сильнее запуталась. Эти нововведения, Нейрониксы, новые программы обучения, учеба сводят меня с ума. С каждым днем, всё сложнее и сложнее сделать выбор.":
            main_character задумчивая1 "— После этого дня я еще сильнее запуталась. Эти нововведения, Нейрониксы, новые программы обучения, учеба сводят меня с ума. С каждым днем, всё сложнее и сложнее сделать выбор."
        "Выступление директора меня вогнало в ступор. Появляются новые программы обучения, а старые уходят в прошлое. Что если и мой выбор IT-профессии будет никому не нужен, ведь появились эти Нейрониксы…":
            main_character задумчивая1 "— Выступление директора меня вогнало в ступор. Появляются новые программы обучения, а старые уходят в прошлое. Что если и мой выбор IT-профессии будет никому не нужен, ведь появились эти Нейрониксы…"
    
    mother обычная1 "— Глупости, еще полно времени, чтобы сделать верный выбор. Не забивай голову пустяками. Сейчас главное окончить школу… Так ведь?!"
    mother обычная1 "— Ты же не собираешься опускаться до 4 и 3? Вся семья ждет от тебя золотую медаль!"
    main_character задумчивая1 "— Да… Но ведь оценки сейчас не самое важное, всё решает экзамен. Именно он определяет, смогу ли я поступить или нет."
    mother обычная1 "— то тебе мешает учиться на отлично и готовиться к экзаменам?"
    main_character задумчивая1 "— Просто в школе так много непрофильных предметов, которые отнимают много времени. Нужно потратить много сил и времени,чтобы знать их на отлично."
    main_character задумчивая1 "— Я думаю лучше это время потратить на подготовку к экзаменам. Раньше было проще учиться на отлично, так как никаких экзаменов не было, а сейчас я ничего не успеваю…"
    mother обычная1 "— Как это - ничего не успеваешь? У тебя единственная задача - окончить школу и сдать экзамены. Это всем, чем ты должна заниматься. Получается, на игры тебе времени хватает, а на учебу нет?"
    main_character задумчивая1 "— Ты не поняла… Ладно, не важно. Я буду в комнате, если что-то потребуется."
    mother обычная1 "— Мы еще вернемся к этому разговору!"

    "*[name_mc] уходит к себе в комнату*"

    jump scene_7

label scene_7:
    scene room
    with slowdissolve1

    "Комната [name_mc]"
    "*[name_mc] сидит за компьютерным столом*"
    "*Она чувствует обиду и злость на  маму, ведь та не понимает, как сложно сейчас ей*"
    show gg обычная
    main_character обычная"Легко сказать: мы ждем от тебя золотую медаль! Сложно сделать, когда на тебя все давят."
    main_character "И игры, это не пустое времяпровождение. Я так отдыхаю, развлекаюсь, забываю о проблемах. В них я вижу спасение, а не пустую трату времени."
    main_character задумчивая"Почему родители не могут понять, что мне сложно? Что я не просто жалуюсь, а просто ищу поддержку…"
    main_character "Разве они сами не были подростками и не знают, как сейчас нелегко мне приходится? Я и так выжимаю из себя максимум, чтобы хорошо учиться, готовиться к экзаменам, быть хорошей дочкой."
    main_character "Похоже, что хуже всего у меня получается последнее."
    main_character угрюмая"Все только и делают, что указывают на мои ошибки. Будто я сама их не вижу."

    "*Нажимает на кнопку запуска компьютера*"
    "*Искра*"

    return










    
        
